AC_INIT(cxxtools, 1.99.5, [Tommi Maekitalo <tommi@tntnet.org>])
AM_INIT_AUTOMAKE

AC_PREREQ([2.5.9])

abi_current=7
abi_revision=0
abi_age=0
sonumber=${abi_current}:${abi_revision}:${abi_age}
AC_SUBST(sonumber)

unset CDPATH

AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_HEADERS([src/config.h ])
AC_CONFIG_FILES([include/cxxtools/config.h])
AC_CONFIG_SRCDIR([src/log.cpp])

AC_HEADER_DIRENT

AC_PROG_CC
AC_PROG_CXX

AC_LANG(C++)

AC_CHECK_HEADERS(sys/filio.h)
AC_CHECK_HEADERS(csignal)

AC_CHECK_LIB(nsl, setsockopt)
AC_CHECK_LIB(socket, accept)
AC_CHECK_LIB(rt, sem_destroy)
AC_SEARCH_LIBS(dlopen, dl, , AC_MSG_ERROR([dlopen not found]))
AC_SEARCH_LIBS(inet_ntop, nsl socket resolv)
AC_CHECK_FUNCS(inet_ntop accept4)
AC_TYPE_LONG_LONG_INT
AC_TYPE_UNSIGNED_LONG_LONG_INT

if test "$ac_cv_type_long_long_int" = yes; then
    HAVE_LONG_LONG=HAVE_LONG_LONG
else
    AC_MSG_WARN([long long not found]);
    HAVE_LONG_LONG=NO_LONG_LONG
fi
AC_SUBST(HAVE_LONG_LONG, "$HAVE_LONG_LONG")

if test "$ac_cv_type_unsigned_long_long_int" = yes; then
    HAVE_UNSIGNED_LONG_LONG=HAVE_UNSIGNED_LONG_LONG
else
    AC_MSG_WARN([unsigned long long not found ($ac_cv_type_unsigned_long_long_int)]);
    HAVE_UNSIGNED_LONG_LONG=NO_UNSIGNED_LONG_LONG
fi
AC_SUBST(HAVE_UNSIGNED_LONG_LONG, "$HAVE_UNSIGNED_LONG_LONG")

AC_SUBST(NETWORK_LIBS)

AC_ARG_WITH([iconvstream],
    AS_HELP_STRING([--with-iconvstream=yes|no], [compile iconv stream (default: no)]),
    [with_iconvstream=$withval],
    [with_iconvstream=no])

if test "$with_iconvstream" = yes; then
  AM_ICONV
  if test "$am_cv_func_iconv" !=  yes; then
    AC_MSG_ERROR(iconv not found);
  fi
fi

AM_CONDITIONAL(MAKE_ICONVSTREAM, test $with_iconvstream = yes)

ACX_PTHREAD

AC_PROG_LIBTOOL
AC_CXXTOOLS_WITH_LOGGING

CXXTOOLS_CXXFLAGS='-I${includedir}'
CXXTOOLS_LDFLAGS='-L${libdir} -lcxxtools'

AC_SUBST(CXXTOOLS_CXXFLAGS)
AC_SUBST(CXXTOOLS_LDFLAGS)
AC_SUBST(CXXTOOLS_LOGGING)

case "${host_cpu}-${host_os}" in
*-aix*)
    SHARED_LIB_FLAG=-qmkshrobj
    ;;
*-darwin*)
    SHARED_LIB_FLAG=-dynamiclib
    ;;
*)  
    SHARED_LIB_FLAG=-shared
    ;;
esac
AC_SUBST(SHARED_LIB_FLAG)

#
# checking inline assembler type for atomic operations
#
AC_CXXTOOLS_ATOMICTYPE

AC_SUBST(CXXTOOLS_ATOMICITY)
AM_CONDITIONAL(MAKE_ATOMICITY_SUN,        test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_SUN)
AM_CONDITIONAL(MAKE_ATOMICITY_WINDOWS,    test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_WINDOWS)
AM_CONDITIONAL(MAKE_ATOMICITY_GCC_ARM,    test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_GCC_ARM)
AM_CONDITIONAL(MAKE_ATOMICITY_GCC_MIPS,   test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_GCC_MIPS)
AM_CONDITIONAL(MAKE_ATOMICITY_GCC_PPC,    test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_GCC_PPC)
AM_CONDITIONAL(MAKE_ATOMICITY_GCC_X86_64, test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_GCC_X86_64)
AM_CONDITIONAL(MAKE_ATOMICITY_GCC_X86,    test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_GCC_X86)
AM_CONDITIONAL(MAKE_ATOMICITY_GCC_SPARC,  test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_GCC_SPARC)
AM_CONDITIONAL(MAKE_ATOMICITY_GCC_AVR32,  test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_GCC_AVR32)
AM_CONDITIONAL(MAKE_ATOMICITY_PTHREAD,    test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_PTHREAD)
AM_CONDITIONAL(MAKE_ATOMICITY_GENERIC,    test "$CXXTOOLS_ATOMICITY" = CXXTOOLS_ATOMICITY_GENERIC)

#
# checking existence of suseconds_t, needed by hirestime.h
#
AC_CHECKING(for suseconds_t)

AC_CHECK_TYPE(
  suseconds_t,
  [CXXTOOLS_SUSECONDS=CXXTOOLS_SUSECONDS_T],
  [CXXTOOLS_SUSECONDS=CXXTOOLS_SUSECONDS_TIME_T],
  [#include <sys/time.h>])

AC_SUBST(CXXTOOLS_SUSECONDS)

AC_COMPILE_IFELSE(
[
    #include <locale>
    void foo() { std::numpunct<char>* np; }
],
[CXXTOOLS_STD_LOCALE=CXXTOOLS_WITH_STD_LOCALE],
[CXXTOOLS_STD_LOCALE=CXXTOOLS_WITHOUT_STD_LOCALE])

AC_SUBST(CXXTOOLS_STD_LOCALE)

TESTDIR="`pwd`/test"
AC_SUBST(TESTDIR)

AC_ARG_ENABLE([demos],
  AS_HELP_STRING([--disable-demos], [disable building demos]),
  [enable_demos=$enableval],
  [enable_demos=enable_demos])

AM_CONDITIONAL(MAKE_DEMOS, test "$enable_demos" = "enable_demos")

AC_ARG_ENABLE([unittest],
  AS_HELP_STRING([--disable-unittest], [disable building unittest]),
  [enable_unittest=$enableval],
  [enable_unittest=enable_unittest])

AM_CONDITIONAL(MAKE_UNITTEST, test "$enable_unittest" = "enable_unittest")

AC_CONFIG_FILES([cxxtools-config], [chmod +x cxxtools-config])

AC_CONFIG_FILES([
  Makefile
  src/Makefile
  src/unit/Makefile
  src/http/Makefile
  src/xml/Makefile
  src/xmlrpc/Makefile
  include/Makefile
  demo/Makefile
  test/Makefile
  ])

AC_OUTPUT
